<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.3">
  <POU Name="FB_PID" Id="{9148ddd9-4f6e-4c4f-98a6-9a43459fe84e}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_PID
VAR_INPUT
	xSet:	REAL; 	// Set value
	xAct:	REAL;	// measured Value	

	Kp  :  	REAL;  	// proportional factor
	Ti  :  	REAL;  	// integral time
	Td  :	REAL; 	// derivative time
		
	LimLow	:	REAL; 	// lower limit of output
	LimHigh	: 	REAL; 	// upper limit of output
	StartY	:	REAL;	// Y to use as offset
	
	bReverseActing 	:	BOOL;	// True for reverse acting
	
	RST		:	BOOL; 	// Reset
END_VAR
VAR_OUTPUT
	Y : REAL;		// controller output
	LIM : BOOL;		// True, if output reaches LimLow or LimHigh
END_VAR
VAR
	e			:  REAL;		// control difference
	yOffset		:  REAL	:= 0;		// constant offset used on Y	
	fbDerivative :  FT_DERIV; 	// function block for derivation
	fbIntegrate  :	FB_Integrate;	// function block for integration
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF StartY <> 0 THEN
	yOffset := StartY;
END_IF

//determine direct or reverse acting
IF bReverseActing THEN
	e := xAct - xSet;
ELSE
	e := xSet - xAct;	
END_IF;

IF Ti>0 THEN
	//derivative and integral calculation
	fbDerivative(in:=e,run:=TRUE);
	fbIntegrate(run:= TRUE, in := e, RST:=RST);
	Y := Kp * (e + 1/Ti * fbIntegrate.out + Td * fbDerivative.out) + yOffset;
ELSE
	//derivative  calculation
	fbDerivative(in:=e,run:=TRUE);
	Y := Kp * (e + Td * fbDerivative.out) + yOffset;
END_IF

//Check limits and Anti-wind up
IF Y < LimLow AND NOT RST THEN
	Y := LimLow;
	LIM := TRUE;
	fbIntegrate(RST:=TRUE, inStart:=(Y/Kp - e)*Ti);
ELSIF Y > LimHigh AND NOT RST THEN
	Y := LimHigh;
	LIM := TRUE;
	fbIntegrate(RST:=TRUE, inStart:=(Y/Kp - e)*Ti);
ELSE
	LIM := FALSE;	
END_IF

(*
pos 23.10.2020:
- no anti-wind up on reset
- possibility to provide an offset value for Y

aku 24.09.2019: first implementation
*)]]></ST>
    </Implementation>
    <LineIds Name="FB_PID">
      <LineId Id="205" Count="0" />
      <LineId Id="207" Count="1" />
      <LineId Id="206" Count="0" />
      <LineId Id="38" Count="0" />
      <LineId Id="9" Count="0" />
      <LineId Id="34" Count="0" />
      <LineId Id="36" Count="1" />
      <LineId Id="35" Count="0" />
      <LineId Id="77" Count="0" />
      <LineId Id="76" Count="0" />
      <LineId Id="106" Count="0" />
      <LineId Id="109" Count="0" />
      <LineId Id="104" Count="0" />
      <LineId Id="80" Count="1" />
      <LineId Id="107" Count="1" />
      <LineId Id="83" Count="0" />
      <LineId Id="79" Count="0" />
      <LineId Id="84" Count="0" />
      <LineId Id="42" Count="0" />
      <LineId Id="85" Count="1" />
      <LineId Id="89" Count="0" />
      <LineId Id="140" Count="0" />
      <LineId Id="91" Count="0" />
      <LineId Id="94" Count="1" />
      <LineId Id="141" Count="0" />
      <LineId Id="96" Count="0" />
      <LineId Id="92" Count="0" />
      <LineId Id="87" Count="0" />
      <LineId Id="99" Count="0" />
      <LineId Id="169" Count="0" />
      <LineId Id="201" Count="2" />
      <LineId Id="200" Count="0" />
      <LineId Id="172" Count="1" />
    </LineIds>
  </POU>
</TcPlcObject>